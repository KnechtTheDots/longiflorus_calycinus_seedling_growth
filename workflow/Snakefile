

rule target:
    input:
        "results/drought_survival.RDS"

# this rule takes the files from data collectio and creates the set 
# of files that will be used for the analysis

rule data_wrangling:
    input:
        day_4="resources/day_4_areas.csv",
        day_17="resources/day_17_areas.csv",
        ids="resources/germ_and_id.csv",
        height="resources/heights.csv",
        drought="resources/seed_growth_vs_seed_size.csv"
    output:
        traits="results/wrangled/traits.csv",
        long="results/wrangled/drought_long.csv",
        short="results/wrangled/drought_short.csv"
    log:
        log="results/logs/data_wrangling.log"
    conda:
        "envs/wrangling.yaml"
    script:
        "scripts/data_wrangling.R"

# run the stan code to fit the growth rate/survival model

rule drought_survival:
    input:
        long="results/wrangled/drought_long.csv",
        short="results/wrangled/drought_short.csv"
    output:
        fit="results/drought_survival.RDS"
    log:
        log="results/logs/drought_survival_model.log"
    params:
        stan="workflow/scripts/stan/drought_survival.stan"
    conda:
        "envs/cmdstanr.yaml"
    script:
        "scripts/drought_survival.R"

rule post_pred_checks:
    input:
        fit="results/drought_survival.RDS",
        short="results/wrangled/drought_short.csv"
    output:
        expand("results/plots/post_pred_checks/{plot}.svg", plot=["pred_vs_obs", "dens", "sd", "mean", "min", "max"]),
        expand("results/plots/rhat_{part}_ess.svg", part=["bulk", "tail"])
    log:
        log="results/logs/post_pred_checks.log"
    conda:
        "envs/plots.yaml"
    script:
        "scripts/ppc_plots.R"

rule survival_plots:
    input:
        fit="results/drought_survival.RDS",
        short="results/wrangled/drought_short.csv"
    output:
        expand("results/plots/survival/{plot}.svg", plot=["survival_vs_size", "survival_vs_rgr", "survival_vs_age_rgr", "survival_vs_seed_rgr", "panel_plot"])
    log:
        log="results/logs/survival_plots.log"
    conda:
        "envs/plots.yaml"
    script:
        "scripts/survival_plots.R"

rule trait_boots:
    input:
        traits="results/wrangled/traits.csv"
    output:
        survive="results/trait_boots/survive.csv",
        day_4="results/trait_boots/day_4.csv",
        day_17="results/trait_boots/day_17.csv",
        rgr="results/trait_boots/rgr.csv",
        height="results/trait_boots/height.csv"
    log:
        log="results/logs/trait_boots.log"
    conda:
        "envs/wrangling.yaml"
    script:
        "scripts/trait_boots.R"

rule epi_plots:
    input:
        survive="results/trait_boots/survive.csv",
        day_4="results/trait_boots/day_4.csv",
        day_17="results/trait_boots/day_17.csv",
        rgr="results/trait_boots/rgr.csv",
        height="results/trait_boots/height.csv",
        traits="results/wrangled/traits.csv"
    output:
        epi="results/plots/epi_test/epi.svg",
        obs="results/plots/epi_test/obs.svg",
        mean_diffs="results/plots/epi_test/mean_diffs.svg"
    log:
        log="results/logs/epi_plots.log"
    conda:
        "envs/plots.yaml"
    script:
        "scripts/epi_plots.R"
